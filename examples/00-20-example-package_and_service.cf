#!/var/cfengine/bin/cf-agent -f-
body file control
{
      inputs => { '$(sys.libdir)/stdlib.cf' };
}

bundle agent example_package_and_service
{
  meta:
    !policy_server::
      "tags" slist => { "autorun" };

  vars:
    centos|redhat::
      "_package" string => "httpd";
      "_service" string => "httpd";

    ubuntu|debian::
      "_package" string => "apache2";
      "_service" string => "apache2";

    any::
      "_default_page"
        string => ifelse( "debian|ubuntu", "/var/www/html/index.html",
                          "centos|redhat", "/var/www/error/noindex.html",
                          "UNKNOWN" ),
        meta => { "inventory", "attribute_name=Apache Default Page"};

  packages:
      "$(_package)"
        policy => "present";

  services:
      "$(_service)"
        service_policy => "start";

  reports:
    inform_mode|verbose_mode::
      "My default page is from $(_default_page)";
}
bundle agent __main{methods:"example_package_and_service";}
